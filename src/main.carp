(load-and-use SDL)
(load "constants.carp")
(load "SDLAudio.carp")
(load "oscillators.carp")
(load "envelope.carp")
(load "channel.carp")
(load "sequencer.carp")
(use SDLAudio)

; siren — embedded audio engine
; signal generation → modulation → processing → mixing → pcm out

; interleave a mono buffer into a stereo output buffer
(defn mono-to-stereo [mono stereo]
  (for [i 0 buf-frames]
    (let [s @(Array.unsafe-nth mono i)]
      (do
        (aset! stereo (* i 2)       s)
        (aset! stereo (+ (* i 2) 1) s)))))

; c minor pentatonic melody
(defn make-melody []
  [(Note.make 262 120)
   (Note.make 311 120)
   (Note.make 349 120)
   (Note.make 392 240)
   (Note.rest 60)
   (Note.make 392 120)
   (Note.make 466 120)
   (Note.make 523 240)
   (Note.rest 120)
   (Note.make 466 120)
   (Note.make 392 120)
   (Note.make 349 240)
   (Note.rest 60)
   (Note.make 311 120)
   (Note.make 262 480)])

(defn main []
  (do
    (SDL.init SDL.INIT_EVERYTHING)
    (let-do [dev    (SDLAudio.open sample-rate channels buf-frames)
             stereo (Array.allocate buf-size)
             ; define the instrument once
             ch     (Channel.make osc-square 5 60 0.0f 80 0.4f)
             seq    (Seq.make (make-melody))]
      (SDLAudio.play dev)
      (println* "siren: running.")
      (while true
        (do
          (while (< (SDLAudio.queued-bytes dev) queue-target)
            (do
              (Seq.tick &seq &ch)
              (Channel.tick &ch)
              (mono-to-stereo (Channel.buf &ch) &stereo)
              (ignore (SDLAudio.queue dev (Array.unsafe-raw &stereo) buf-size))))
          (SDL.delay 1))))))
